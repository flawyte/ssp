apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'signing'

buildscript {
    repositories {
        mavenCentral()
        maven {
            url 'https://maven.google.com/'
            name 'Google'
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.3.1'
    }
}

repositories {
    mavenCentral()
    maven {
        url 'https://maven.google.com/'
        name 'Google'
    }
}

android {
    namespace 'com.intuit.ssp'
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 34
        compileSdk 34
    }
}

class SSPFactory extends DefaultTask {

    @Input
    String unit = "sp"
    @Input
    double positiveMax = 100

    @TaskAction
    def create() {
        String resFolder =  project.getProjectDir().getPath() + "/src/main/res/";
        for(double dimen = 300; dimen <= 1080 ; dimen = dimen + 30){
            createPositive(resFolder, dimen)
        }
    }

    private void createPositive(String resFolder, double dimen) {
        String folder = resFolder + "values-sw" + (int) dimen + "dp";
        String fileName = folder + "/positive_ssps.xml";
        new File(folder).mkdir();
        new File(fileName).createNewFile();
        PrintWriter printWriter = new PrintWriter(fileName);
        printWriter.println("<?xml version=\"1.0\" encoding=\"utf-8\"?>");
        printWriter.println("<resources>");
            for(int i=1; i<=positiveMax; i++){
            double ratio = i / 300d;
            double ssp = ratio * dimen;
            printWriter.printf("\t<dimen name=\"_%dssp\">%.2f" + unit + "</dimen>\r\n", i, ssp);
        }
        printWriter.println("</resources>");
        printWriter.close();
    }
}

task createSSP(type: SSPFactory) {
}

//uncomment next line to edit values
//preBuild.dependsOn createSSP

createSSP{
    unit = "sp"
    positiveMax = 100//change to 600 or any other value if needed
}

publishing {
    publications {
        release(MavenPublication) {
            groupId = 'com.intuit.ssp'
            artifactId = 'ssp-android'
            version = '1.1.1'
            artifact("$buildDir/outputs/aar/ssp-android-release.aar")
            pom {
                name = 'ssp'
                description = 'A scalable size unit for Andorid'
                url = 'https://github.com/intuit/ssp'
                licenses {
                    license {
                        name = 'The MIT License (MIT)'
                        url = 'https://github.com/intuit/ssp/blob/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = developerID
                        name = developerName
                        email = developerEmail
                    }
                }
                scm {
                    connection = 'scm:git@github.com:intuit/ssp.git'
                    developerConnection = 'scm:git@github.com:intuit/ssp.git'
                    url = 'https://github.com/intuit/ssp'
                }
            }
        }
    }
    repositories {
        maven {
            url sonatypeRepo
            credentials {
                username sonatypeUsername
                password sonatypePassword
            }
        }
    }
}

signing {
    sign publishing.publications.release
}
